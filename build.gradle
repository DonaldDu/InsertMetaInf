// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.4.30"
//    ext.testPlugin = true
    repositories {
        google()
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        if (project.hasProperty('testPlugin')) classpath "com.github.DonaldDu:InsertMetaInf:1.0"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects { project ->
    ext.publishGAV = { String gav ->
        apply plugin: 'maven-publish'
        project.afterEvaluate {
            publishing {
                publications {
                    maven(MavenPublication) {
                        if (project.plugins.hasPlugin('com.android.library')) {
                            from components.release
                        } else if (project.plugins.hasPlugin('java-library')) {
                            from components.java
                        } else if (project.plugins.hasPlugin('java')) {
                            from components.java
                        }
                        def data = gav.split(":")
                        groupId = data[0]
                        artifactId = data[1]
                        version = data[2]
                    }
                }
            }
        }
    }
}